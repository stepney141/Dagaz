(define pawn-shift (
  $1 (verify empty?)
  (verify (in-zone? board-zone))
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define pawn-jump (
  $1
  (verify empty?)
  (verify (in-zone? third-rank))
  $1 (verify empty?)
  (verify (in-zone? board-zone))
  add
))

(define pawn-step (
  $1 (verify enemy?)
  (verify (in-zone? board-zone))
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define en-passant (
  $1
  (verify enemy?)
  (verify last-to?)
  (verify (piece? Pawn))
  capture
  n mark n
  (verify last-from?)
  back
  add
))

(define step (
  $1 (verify not-friend?)
  (verify (in-zone? board-zone))
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  (verify (in-zone? board-zone))
  add
))

(define slide (
  $1 
  (while empty? (verify (in-zone? board-zone)) add $1)
  (verify not-friend?)
  (verify (in-zone? board-zone))
  add
))

(define O-O (
  (verify (in-zone? init-king))
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify friend?) (verify (piece? Rook))
  (verify (in-zone? init-rook))
  cascade
  $2 $2
  add
))

(define O-O-O (
  (verify (in-zone? init-king))
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify empty?)
  $1 (verify friend?) (verify (piece? Rook))
  (verify (in-zone? init-rook))
  cascade
  $2 $2 $2
  add
))

(game
  (title "Chess")

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/sandbox/3x3.bmp")
     (grid
         (start-rectangle 2 2 70 70)
         (dimensions
           ("a/b/c/d/e/f/g/h" (68 0)) ; files
           ("1/2/3/4/5/6/7/8" (0 68)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name last-rank) (players White)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
     (zone (name last-rank) (players Black)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name third-rank) (players White)
           (positions a6 b6 c6 d6 e6 f6 g6 h6)
     )
     (zone (name third-rank) (players Black)
           (positions a3 b3 c3 d3 e3 f3 g3 h3)
     )
     (zone (name init-king) (players White)
           (positions e8)
     )
     (zone (name init-king) (players Black)
           (positions e1)
     )
     (zone (name init-rook) (players White)
           (positions a8 h8)
     )
     (zone (name init-rook) (players Black)
           (positions a1 h1)
     )
     (zone (name board-zone) (players White Black)
           (positions 
                 a1 b1 c1 d1 e1 f1 g1 h1 
                 a2 b2 c2 d2 e2 f2 g2 h2 
                 a3 b3 c3 d3 e3 f3 g3 h3 
                 a4 b4 c4 d4 e4 f4 g4 h4
                 a5 b5 c5 d5 e5 f5 g5 h5
                 a6 b6 c6 d6 e6 f6 g6 h6
                 a7 b7 c7 d7 e7 f7 g7 h7
                 a8 b8 c8 d8 e8 f8 g8 h8
           )
     )
  )

  (piece
     (name Pawn)
     (image White "images/sandbox/wpawn.bmp"
            Black "images/sandbox/bpawn.bmp")
     (moves
         (pawn-shift n) (pawn-jump n) 
         (pawn-step nw) (pawn-step ne)
         (en-passant e) (en-passant w)
     )
  )
  (piece
     (name Rook)
     (image White "images/sandbox/wrook.bmp"
            Black "images/sandbox/brook.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Knight)
     (image White "images/sandbox/wknight.bmp"
            Black "images/sandbox/bknight.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Bishop)
     (image White "images/sandbox/wbishop.bmp"
            Black "images/sandbox/bbishop.bmp")
     (moves
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name Queen)
     (image White "images/sandbox/wqueen.bmp"
            Black "images/sandbox/bqueen.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name King)
     (image White "images/sandbox/wking.bmp"
            Black "images/sandbox/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
         (O-O e w) (O-O-O w e)
     )
  )

  (board-setup
    (White 
         (King b3)
    )
    (Black 
         (King b1)
    )
  )
)
